(;(--- just CEKin, or: an abstract machine for KARAKON ---)

(('lookup $k (($k . ?v) .   _ )) => `(JUST ,v))
(('lookup $k (( _ . _ ) . ?bnd)) => ^(lookup ,k ,bnd))
(('lookup  _ ()                ) => `(NONE))

(('apd ()         ?ys) => ys)
(('apd (?x . ?xs) ?ys) => `(,x . ,^(apd ,xs ,ys)))

(('rev ?xs           ) => ^(rev ,xs ()))
(('rev ()         ?rs) => rs)
(('rev (?x . ?xs) ?rs) => ^(rev ,xs (,x . ,rs)))


(('match? ?pat ?exp) => ^(match? ,pat ,exp ()))

(('match? ()          ()         ?bnd) => bnd)
(('match? '_           _         ?bnd) => bnd)
(('match? ('quote $e) $e         ?bnd) => bnd)
(('match? ('sym $sv)  $e         ?bnd) => ^(match-var ,sv ,e ,bnd))
(('match? ('exp $ev)  ?e         ?bnd) => ^(match-var ,ev ,e ,bnd))
(('match? (?p . ?ps)  (?e . ?es) ?bnd) => ^(match?* ,^(match? ,p ,e ,bnd) ,ps ,es))
(('match? _           _          _) => 'NO-MATCH)

(('match?* 'NO-MATCH _  _ ) => 'NO-MATCH)
(('match?* ?bnd      ?p ?e) => ^(match? ,p ,e ,bnd))

(('match-var $v ?e ?bnd) => ^(match-var* ,^(lookup ,v ,bnd) ,v ,e ,bnd))
(('match-var* ('NONE)    $v ?e ?bnd) => `((,v . ,e) . ,bnd))

(('match-var* ('JUST ?e) $v ?e ?bnd) => bnd)
(('match-var* ('JUST _)   _  _   _ ) => 'NO-MATCH)

(('unquotes-of ?qq) => ^(unquotes-of ,qq ()))

(('unquotes-of ('unquote ?e) ?adr) => `((,e ,^(rev ,adr))))
(('unquotes-of (?h . ?t)     ?adr) => ^(apd ,^(unquotes-of ,h (A . ,adr)) ,^(unquotes-of ,t (D . ,adr))))
(('unquotes-of _             _   ) => ())

(('subst ?e 'at ()        'in _        ) => e)
(('subst ?e 'at ('A . ?a) 'in (?h . ?t)) => `(,^(subst ,e at ,a in ,h) . ,t))
(('subst ?e 'at ('D . ?a) 'in (?h . ?t)) => `(,h . ,^(subst ,e at ,a in ,t)))

((('step ?prg) ((?r) _                               ())) => `(HALT ,r))

((('step ?prg) (?C   (?e . ?E) ($var              . ?K))) => ^(LU-step (,C (,e . ,E) ,K) ,var ,^(lookup ,var ,e)))
((('step ?prg) (?C   ?E        (('quote $s)       . ?K))) => `((,s . ,C) ,E ,K))
((('step ?prg) (?C   ?E        (()                . ?K))) => `((() . ,C) ,E ,K))
((('step ?prg) (?C   ?E        (('quasiquote ?qq) . ?K))) => `(,C ,E ((K-qq ,^(unquotes-of ,qq) ,qq) . ,K)))
((('step ?prg) (?C   ?E        (('rec ?qq)        . ?K))) => `(,C ,E ((K-qq ,^(unquotes-of ,qq) ,qq) (K-rec) . ,K)))

((('step ?prg) (?C        ?E (('K-qq ()              ?qq) . ?K))) => `((,qq . ,C) ,E ,K))
((('step ?prg) (?C        ?E (('K-qq ((?u ?a) . ?us) ?qq) . ?K))) => `(,C ,E (,u (K-qq ((,a) . ,us) ,qq) . ,K)))
((('step ?prg) ((?c . ?C) ?E (('K-qq ((?a)    . ?us) ?qq) . ?K))) => `(,C ,E ((K-qq ,us ,^(subst ,c at ,a in ,qq)) . ,K)))

((('step ?prg) ((?c . ?C)       ?E (('K-rec)     . ?K))) => ^(M-step ,prg (,C ,E ,K) ,c))
((('step ?prg) (?C        (_ . ?E) (('K-clean-E) . ?K))) => `(,C ,E ,K))

(('LU-step _          $var ('NONE)   ) => `(HALT (UNBOUND VARIABLE ,var)))
(('LU-step (?C ?E ?K) _    ('JUST ?e)) => `((,e . ,C) ,E ,K))

(('M-step ()                          _ ?exp) => `(HALT (NO MATCH FOR ,exp)))
(('M-step ((?lhs '=> ?rhs) . ?prg) ?CEK ?exp) => ^(M-step* ,prg ,rhs ,CEK ,^(match? ,lhs ,exp) ,exp))
(('M-step ((?lhs ?rhs)     . ?prg) ?CEK ?exp) => ^(M-step* ,prg ,rhs ,CEK ,^(match? ,lhs ,exp) ,exp)) ;(- hehe -)

(('M-step* ?prg _    ?CEK       'NO-MATCH ?exp) => ^(M-step ,prg ,CEK ,exp))
(('M-step* ?prg ?rhs (?C ?E ?K) ?bnd      _   ) => `(,C (,bnd . ,E) (,rhs (K-clean-E) . ,K)))

((('run* ?step) ('HALT ?msg)) => msg)
((('run* ?step) ?CEK        ) => ^((run* ,step) ,^(,step ,CEK)))

(('run-program ?exp ?program) => ^((run* (step ,program)) (() () ((rec ,exp)))))

)
