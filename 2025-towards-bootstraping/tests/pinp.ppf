(
 (('match? (exp pattern) (exp expression))
  (& (match? ,pattern ,expression ())))
 (('match? () () (exp bnd)) bnd)
 (('match? 'T 'T (exp bnd)) bnd)
 (('match? '_ _ (exp bnd)) bnd)
 (('match? (num n) (num n) (exp bnd)) bnd)
 (('match? ('quote (exp e)) (exp e) (exp bnd)) bnd)
 (('match? ('num (sym nv)) (num e) (exp bnd)) (& (match-var ,nv ,e ,bnd)))
 (('match? ('num _) _ _) 'NO-MATCH)
 (('match? ('sym (sym sv)) (sym e) (exp bnd)) (& (match-var ,sv ,e ,bnd)))
 (('match? ('sym _) _ _) 'NO-MATCH)
 (('match? ('atm (sym av)) (atm e) (exp bnd)) (& (match-var ,av ,e ,bnd)))
 (('match? ('atm _) _ _) 'NO-MATCH)
 (('match? ('exp (sym ev)) (exp e) (exp bnd)) (& (match-var ,ev ,e ,bnd)))
 (('match? ((exp p) exp ps) ((exp e) exp es) (exp bnd))
  (& (match?* ,(& (match? ,p ,e ,bnd)) ,ps ,es)))
 (('match? _ _ _) 'NO-MATCH)
 (('match?* 'NO-MATCH _ _) 'NO-MATCH)
 (('match?* (exp bnd) (exp p) (exp e)) (& (match? ,p ,e ,bnd)))
 (('match-var (sym v) (exp e) (exp bnd))
  (& (match-var* ,(& (lookup ,v ,bnd)) ,v ,e ,bnd)))
 (('match-var* ('NONE) (sym v) (exp e) (exp bnd))
  `((,v unquote e) unquote bnd))
 (('match-var* ('JUST (exp e)) (sym v) (exp e) (exp bnd)) bnd)
 (('match-var* ('JUST _) _ _ _) 'NO-MATCH)
 (('lookup (sym k) (((sym k) exp v) . _)) `(JUST ,v))
 (('lookup (sym k) ((_ . _) exp bnd)) (& (lookup ,k ,bnd)))
 (('lookup _ ()) '(NONE))
 (('value () _ _) ())
 (('value 'T _ _) T)
 (('value (num n) _ _) n)
 (('value ('quote (exp e)) _ _) e)
 (('value ('quasiquote (exp qq)) (exp bnd) (exp app))
  (& (val-qq ,qq ,bnd ,app)))
 (('value ('+ (exp e) (exp e*)) (exp bnd) (exp app))
  (+ (& (value ,e ,bnd ,app)) (& (value ,e* ,bnd ,app))))
 (('value ('- (exp e) (exp e*)) (exp bnd) (exp app))
  (- (& (value ,e ,bnd ,app)) (& (value ,e* ,bnd ,app))))
 (('value ('* (exp e) (exp e*)) (exp bnd) (exp app))
  (* (& (value ,e ,bnd ,app)) (& (value ,e* ,bnd ,app))))
 (('value ('& (exp a)) (exp bnd) (exp app))
  (& (,app ,(& (val-qq ,a ,bnd ,app)))))
 (('value (sym k) (exp bnd) _) (& (val-lo ,k ,(& (lookup ,k ,bnd)))))
 (('val-lo (sym k) ('NONE)) `(unbound ,k))
 (('val-lo _ ('JUST (exp v))) v)
 (('val-qq ('unquote (exp e)) (exp bnd) (exp app)) (& (value ,e ,bnd ,app)))
 (('val-qq ((exp e) exp es) (exp bnd) (exp app))
  `(,(& (val-qq ,e ,bnd ,app)) unquote (& (val-qq ,es ,bnd ,app))))
 (('val-qq (exp e) _ _) e)
 (('pindolf (exp e) (exp prg)) (& (pindolf ,e ,prg ,prg)))
 (('pindolf (exp e) () _) `(NO-MATCH ,e))
 (('pindolf (exp e) (((exp pat) (exp exp)) exp prg*) (exp prg))
  (& (pindolf* ,e ,(& (match? ,pat ,e)) ,exp ,prg* ,prg)))
 (('pindolf* (exp e) 'NO-MATCH _ (exp prg*) (exp prg))
  (& (pindolf ,e ,prg* ,prg)))
 (('pindolf* _ (exp bnd) (exp e*) _ (exp prg)) (& (value ,e* ,bnd (app ,prg))))
 ((('app (exp prg)) (exp e)) (& (pindolf ,e ,prg)))
)
