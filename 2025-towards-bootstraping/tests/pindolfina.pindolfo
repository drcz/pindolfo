(
 ;(- from lists.pindolfo ---------------------------------------------------------)
 (('take  0 'from _         ) => ())
 (('take #n 'from (?x . ?xs)) => `(,x . ,^(take ,(- n 1) from ,xs)))

 (('drop  0 'from ?xs       ) => xs)
 (('drop #n 'from (?x . ?xs)) => ^(drop ,(- n 1) from ,xs))

 (('lookup    _ 'in ()                    ) => 'NOT-FOUND)
 (('lookup ?key 'in ((?key ?val) .      _)) => val)
 (('lookup ?key 'in (          _ . ?alist)) => ^(lookup ,key in ,alist))

 ;(- ,,adressors'' ---------------------------------------------------------------)
 (('extract           () 'from ?e      ) => e)
 (('extract ('A . ?addr) 'from (?e . _)) => ^(extract ,addr from ,e))
 (('extract ('D . ?addr) 'from (_ . ?e)) => ^(extract ,addr from ,e))

 ;(- machine big step ------------------------------------------------------------)
 (('run-program ?expr ?program) => ^(step* (() (,expr) (((CALL DISPATCH0 1)))) ,program))

 (('step* ( _ (?r . _) ())    _) => r)
 (('step* ('FAIL ?msg)        _) => msg)
 (('step* (?D ?R       ?C) ?prg) => next <- ^(step ,D ,R ,C ,prg)
                                    ^(step* ,next ,prg))

 ;(- machine small step ----------------------------------------------------------)
 (('step ?D ?R ((('CALL $id #n) . ?c) . ?C) ?prg) => args <- ^(take ,n from ,R)
                                                     c* <- ^(lookup ,id in ,prg)
                                                     R* <- ^(drop ,n from ,R)
                                                     `((,args . ,D) ,R* (,c* ,c . ,C)))

 (('step ?D (?r . ?R) ((('FAIL) . _) . _) _) => `(FAIL (NO MATCH FOR ,r)))

 (('step (_ . ?D) ?R (() . ?C) _) => `(,D ,R ,C))
 (('step () ?R (() . ?C) _) => `(() ,R ,C))

 (('step (?d . ?D) ?R ((('IF . ?ts)) . ?C) _) => c* <- ^(dispatch (IF . ,ts) wrt ,d)
                                                 `((,d . ,D) ,R (,c* . ,C)))

 (('step (?d . ?D) ?R ((('LD ?addr) . ?c) . ?C) _) =>  e <- ^(extract ,addr from ,d)
                                                       `((,d . ,D) (,e . ,R) (,c . ,C)))

 (('step (?d . ?D) (?r . ?R) ((('ST) . ?c) . ?C) _) => `(((,r . ,d) . ,D) ,R (,c . ,C)))

 (('step ?D ?R ((('CONST ?e) . ?c) . ?C) _) => `(,D (,e . ,R) (,c . ,C)))

 (('step ?D (?h ?t . ?R) ((('CONS) . ?c) . ?C) _) => `(,D ((,h . ,t) . ,R) (,c . ,C)))
 (('step ?D ((?a . _ ) . ?R) ((('CAR) . ?c) . ?C) _) => `(,D (,a . ,R) (,c . ,C)))
 (('step ?D (( _ . ?d) . ?R) ((('CDR) . ?c) . ?C) _) => `(,D (,d . ,R) (,c . ,C)))
 (('step ?D (#n #m . ?R) ((('ADD) . ?c) . ?C) _) =>  `(,D (,(+ n m) . ,R) (,c . ,C)))
 (('step ?D (#n #m . ?R) ((('SUB) . ?c) . ?C) _) =>  `(,D (,(- n m) . ,R) (,c . ,C)))
 (('step ?D (#n #m . ?R) ((('MUL) . ?c) . ?C) _) =>  `(,D (,(* n m) . ,R) (,c . ,C)))
 (('step ?D (#n #m . ?R) ((('DIV) . ?c) . ?C) _) =>  `(,D (,(/ n m) . ,R) (,c . ,C)))
 (('step ?D (#n #m . ?R) ((('MOD) . ?c) . ?C) _) =>  `(,D (,(% n m) . ,R) (,c . ,C)))
 (('step ?D (#n #m . ?R) ((('LT) . ?c)  . ?C) _) =>  `(,D (,(< n m) . ,R) (,c . ,C)))

 ;(- dispatch stuff --------------------------------------------------------------)
 (('dispatch ('IF ?req ?tb ?fb) 'wrt ?d) => res <- ^(test ,req wrt ,d)
                                            ^(dispatch* ,res ,tb ,fb wrt ,d))

 (('dispatch ?leaf             'wrt  _) => leaf)

 (('dispatch* 'YES ?tb   _ 'wrt ?d) => ^(dispatch ,tb wrt ,d))
 (('dispatch* 'NO    _ ?fb 'wrt ?d) => ^(dispatch ,fb wrt ,d))

 ;(- pretty adhoc but the problem is freshly discovered and we DO need this: -)
 (('test ('and ?t ?t*) 'wrt ?d) => res <- ^(test ,t wrt ,d)
                                   ^(aaand-test ,res ,t* ,d))

 (('aaand-test 'YES ?t* ?d) => ^(test ,t* wrt ,d))
 (('aaand-test 'NO _ _) => 'NO)

 ;(- even more adhoc but i did't yet come up with anything better...  -)
 ;(- although if it stays, the computation should be prolly done on R -)
 ;(- with test code more similar to leaf code ofc. we'll see          -)
 (('test ('<calc> ('EQ? ?e0 ?e1)) 'wrt ?d) => v0 <- ^(t-calc ,e0 wrt ,d)
                                              v1 <- ^(t-calc ,e1 wrt ,d)
                                              ^(EQ? ,v0 ,v1))

 (('t-calc ('+ ?e0 ?e1) 'wrt ?d) => (+ ^(t-calc ,e0 wrt ,d) ^(t-calc ,e1 wrt ,d)))
 (('t-calc ('- ?e0 ?e1) 'wrt ?d) => (- ^(t-calc ,e0 wrt ,d) ^(t-calc ,e1 wrt ,d)))
 (('t-calc ('* ?e0 ?e1) 'wrt ?d) => (* ^(t-calc ,e0 wrt ,d) ^(t-calc ,e1 wrt ,d)))
 (('t-calc ('/ ?e0 ?e1) 'wrt ?d) => (/ ^(t-calc ,e0 wrt ,d) ^(t-calc ,e1 wrt ,d)))
 (('t-calc ('% ?e0 ?e1) 'wrt ?d) => (% ^(t-calc ,e0 wrt ,d) ^(t-calc ,e1 wrt ,d)))
 (('t-calc #n 'wrt _) => n)
 (('t-calc ?addr 'wrt ?d) => ^(extract ,addr from ,d))

 (('test (?test ?addr)        'wrt ?d) => ^(,test ,^(extract ,addr from ,d)))

 (('test ('EQ? #n ?addr)          'wrt ?d) => ^(EQ? ,n ,^(extract ,addr from ,d)))
 (('test ('EQ? ('quote ?e) ?addr) 'wrt ?d) => ^(EQ? ,e ,^(extract ,addr from ,d)))
 (('test ('EQ? ?addr ?addr*)      'wrt ?d) => ^(EQ? ,^(extract ,addr from ,d)
                                                    ,^(extract ,addr* from ,d)))

 (('NIL? ()) => 'YES)
 (('NIL?  _) => 'NO)

 (('CONS? (_ . _)) => 'YES)
 (('CONS? _      ) => 'NO)

 (('NUM? #n) => 'YES)
 (('NUM?  _) => 'NO)

 (('SYM? $s) => 'YES)
 (('SYM?  _) => 'NO)

 (('ATM? (_ . _)) => 'NO)
 (('ATM? _      ) => 'YES)

 (('EQ? ?x ?x) => 'YES)
 (('EQ? _  _ ) => 'NO)
 
)
